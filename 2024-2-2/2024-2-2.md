# 2024-2-2

## Souffle

edge是一个输入关系，将从磁盘读取

path 是一个输入关系，将被写入磁盘

如果有一条从 x 到 y 的边，那么就有一条从 x 到 y 的路径

如果有一条从 x 到 z 的路径，并且从 z 到 y 有一条边，那么就有一条从 x 到 y 的路径

```
.decl edge(x:number, y:number)  // 声明关系
.input edge     // 输入

.decl path(x:number, y:number)  // 声明关系
//进行关系定义
path(x, y) :- edge(x, y).
path(x, y) :- path(x, z), edge(z, y).
.output path    // 输出
```

算术表达式

Soufflé 中允许使用以下算术函子：

- 加法：`x + y`
- 减法：`x - y`
- 除法：`x / y`
- 乘法：`x * y`
- 模数：`a % b`
- 幂：`a ^ b`
- 计数器：`autoinc()`
- 位操作：`x band y`、`x bor y`、`x bxor y` 和 `bnot x`
- 逻辑运算：`x land y`、`x lor y` 和 `lnot x`

Soufflé 中允许以下算术约束：

- 小于：`a < b`
- 小于或等于：`a <= b`
- 等于：`a = b`
- 不等于：`a != b`
- 大于或等于：`a >= b`
- 大于：`a > b`

打印斐波那契数列：

```
.decl Fib(i:number, a:number)
Fib(1, 1).
Fib(2, 1).
Fib(i + 1, a + b) :- Fib(i, a), Fib(i-1, b), i < 10.
.output Fib
```

数字可以写成十进制、二进制和十六进制：

```
.decl A(x:number)
A(4711).
A(0b101).
A(0xaffe).
```

函子 autoinc()相当于random，在每次求值时都会产生一个新数字，但是不允许在递归关系中使用。

```
.decl A(x: symbol)
A(“a”). A(“b”). A(“c”). A(“d”).

.decl B(x: symbol, y: number)
B(x, autoinc()) :- A(x).    // 为 a、b、c、d 各生成一个标识符
.output B
```

计数函子用于计算子目标的集合大小，语法为 `count:{<sub-goal>}`。

```
.decl Car(car_master: symbol, colour:symbol)
Car("Wang", "blue").
Car("Li", "red").
Car("Zhao", "blue").

.decl BlueCarCount(x: number)
BlueCarCount(c) :- c = count:{Car(_, "blue")}.
.output BlueCarCount
```

max 函子输出集合的最大值，语法为 `max <var>:{<sub-goal(<var>)>}`，最小值语法为 `min <var>:{<sub-goal>(<var>)>}`；求和语法为 `sum <var>:{<sub-goal>(<var>)>}`。

```
.decl A(n:number)
A(1). A(10). A(100).

.decl MaxA(x: number)
MaxA(y) :- y = max x:{A(x)}.
.output MaxA
```



## 参考

该项目参考The Eye of Horus: Spotting and Analyzing Attacks on Ethereum Smart Contracts所实现的基于Datalog的区块链漏洞分析。

该项目将整个流程分为三个步骤，分别是提取、分析和追踪

（1）提取：将区块链中的与执行相关的信息并将其存储为Datalog中的Facts。

（2）分析：Facrs输入经过提前设置好的Datalog的Realtions和Queries进行处理最后得到Result并输出。

（3）追踪：追踪阶段检索通过分析获得的攻击者帐户列表，并获取与这些帐户相关的所有交易。之后，创建一个图形数据库，该数据库捕获了来自这些账户的资金流。此外，可以用标记帐户列表来扩充数据库，以增强对被盗资产的追踪。

由于该项目的第三步追踪与本项目无关，所以本项目将借鉴该项目的前两步来完成程序执行相关信息的提取和漏洞分析

### 提取



### 分析

#### 基础定义

edb.dl

```c++
.decl def(step:number, opcode:Opcode, transaction_hash:symbol)
.decl use(step1:number, step2:number, transaction_hash:symbol)
.decl arithmetic(step:number, opcode:Opcode, first_operand:Value, second_operand:Value, arithmetic_result:Value, evm_result:Value)
.decl storage(step:number, opcode:Opcode, transaction_hash:symbol, caller:Address, contract:Address, storage_index:Value, storage_value:Value, depth:number)//SLOAD和SSTORE操作
.decl condition(step:number, transaction_hash:symbol)
.decl transfer(step:number, transaction_hash:symbol, contract:Address, from:Address, to:Address, value:Value)//转账
.decl call(step:number, transaction_hash:symbol, opcode:Opcode, caller:Address, callee:Address, input_data:symbol, amount:Value, depth:number, call_id:number, call_branch:number, success:number)//合约调用
.decl throw(step:number, transaction_hash:symbol, opcode:Opcode, caller:Address, depth:number)
.decl selfdestruct(step:number, transaction_hash:symbol, caller:Address, contract:Address, destination:Address, amount:Value)
.decl error(transaction_hash:symbol, error_message:symbol)//交易报错
.decl block(block:number, gas_used:number, gas_limit:number, timestamp:number)//区块
.decl transaction(transaction_hash:symbol, transaction_index:number, block_number:number, from:Address, to:Address, input_data:symbol, gas_used:number, gas_limit:number, status:number)//交易
//输入：将从区块链中提取出来的信息进行输入，类似于给各个表进行信息导入
.input def
.input use
.input arithmetic
.input storage
.input condition
.input transfer
.input call
.input throw
.input selfdestruct
.input error
.input block
.input transaction
```

将区块链上的如交易、调用等操作抽象出来声明为多种关系，类似于建立多个表，并将提取的具体区块链信息进行输入。

types.dl

```c++
.type Address//地址
.type Opcode//Opcode操作码
.type Value//一种堆栈值
```

定义三种新类型

horus.dl

```c
#include "types.dl"
#include "edb.dl"

.decl data_flow(x:number, y:number, hash:symbol)//数据流
data_flow(x, y, hash) :-
  use(y, x, hash).

data_flow(x, y, hash) :-
  use(y, z, hash),
  data_flow(x, z, hash).

.decl used_in_condition(step:number, hash:symbol)
used_in_condition(step1, hash) :-
  condition(step2, hash),
  data_flow(step1, step2, hash).

.decl caller_check(step:number, hash:symbol)//检查步骤的调用者
caller_check(step1, hash) :-
  def(step1, "CALLER", hash),
  used_in_condition(step1, hash).

.decl opcode(step:number, opcode:Opcode, hash:symbol)
opcode(step, opcode, hash) :-
  def(step, opcode, hash).
```

进一步定义一些关系

#### 漏洞分析

```c++
.decl Reentrancy(hash:symbol, timestamp:number, step:number, attacker:Address, caller:Address, callee:Address, depth:number, amount:Value)
Reentrancy(hash, timestamp, step3, attacker, caller, callee, depth2, amount) :-
  storage(step1,  "SLOAD", hash, _, caller, index, _, depth1),//状态读取
  call(step2, hash, opcode, caller, callee, input_data,      _, depth1, call_id, call_branch, 1),//第一次调用
  call(step3, hash, opcode, caller, callee, input_data, amount, depth2, call_id, call_branch, 1),//重入调用
  storage(step4, "SSTORE", hash, _, caller, index, _, depth1),//状态写入
  depth1 < depth2,//检查调用深度
  step1 < step2,//检查步骤顺序
  step3 < step4,
  !match("0", amount),//所获取的钱数量是否大于0
  transaction(hash, _, block, attacker, _, _, _, _, 1),//进行了交易
  block(block, _, _, timestamp).//交易所在区块
.output Reentrancy
```

**重入：**攻击合约调用被攻击合约时，在更新被攻击合约中的状态之前，被攻击合约触发回退函数，攻击合约利用回退函数继续提款，就会发生重入。首先检查两个成功的调用是否共享相同的交易哈希，调用者、被调用者、ID和分支，如果第二个调用的调用深度比第一个调用的调用深度高，并且两个存储操作具有与第一个调用相同的调用深度，其中第一个操作是SLOAD并发生在第一个调用之前，而第二个操作是SSTORE并发生在第二个调用之后。就证明发生了重入攻击

```c
.decl ParityWalletHack1(hash1:symbol, hash2:symbol, timestamp1:number, timestamp2:number, caller:Address, contract:Address, callee:Address, amount:Value)
ParityWalletHack1(hash1, hash2, timestamp1, timestamp2, from, caller, callee, amount) :-
  transaction(hash1, index1, block1, from, to, input_data1, _, _, 1),//检查两起交易
  substr(input_data1, 0, 8) = "e46dcfeb",//检查交易的签名
  transaction(hash2, index2, block2, from, to, input_data2, _, _, 1),
  substr(input_data2, 0, 8) = "b61d27f6",
  call(_, hash2, "CALL", caller, callee, _, amount, _, _, _, 1),
  (block1 < block2;//检查是否在t1之后执行t2，如果在同一区块检查序号
  block1 = block2, index1 < index2),
  block(block1, _, _, timestamp1),//交易所在区块
  block(block2, _, _, timestamp2).
.output ParityWalletHack1
```

**Parity钱包破解（Parity wallet hacks）：**Parity钱包破解是由于实施错误的访问控制而导致攻击者可以将自己设置为所有者，从而使他们能够执行关键行动。该漏洞的原理是库合约的函数被调用时，它是运行在调用方的上下文里。而为了能被用户合约调用，本次事件中的库合约初始化函数都是 public 的。当黑客直接调用了库合约的初始化函数时，由于库合约本质上也不过是另一个智能合约，这次攻击调用使用的是库合约本身的上下文。对于调用者而言，这个库合约是未经初始化的，而黑客通过初始化参数把自己设置的成了 owner，接下来又作为 owner 调用了 kill 函数，抹除了库合约的所有代码。

首先我们要检查是否存在两个交易t1和t2都包含相同的发起者和接收者，其中t1输入的前4个字节与initWallet函数的函数签名（即e46dcfeb）匹配，以及t2输入的前4个字节与execute函数的函数签名（即b61d27f6）匹配。然后，检查是否存在一个call，该调用是t2的一部分，并且在t1之后执行t2。

```c++
.decl IntegerOverflow(hash:symbol, timestamp:number, step:number, opcode:Opcode, first_operand:Value, second_operand:Value, arithmetic_result:Value, evm_result:Value, attacker:Address, contract: Address, from:Address, to:Address, amount:Value)
IntegerOverflow(hash, timestamp, step4, opcode, first_operand, second_operand, arithmetic_result, evm_result, attacker, contract, from, to, amount) :-
  (opcode(step1, "CALLDATALOAD", hash);
  opcode(step1, "CALLDATACOPY", hash)),//获取整数
  arithmetic(step2, opcode, first_operand, second_operand, arithmetic_result, evm_result),//进行算术运算
  arithmetic_result != evm_result,//结果与以太坊虚拟机的结果不同
  storage(step3, "SSTORE", hash, _, _, _, _, 1),//将溢出的整数存储下来了
  transfer(step4, hash, contract, from, to, amount),//进行了交易
  !match("0", amount),//并且交易的数量不为0
  (amount = first_operand;//交易的数为算数运算中的第一或第二个参数
  amount = second_operand),
  data_flow(step1, step2, hash),//算术运算的参数和存储的数是否为第一部输入流入的数据
  data_flow(step2, step3, hash),
  transaction(hash, _, block, attacker, _, _, _, _, 1),//交易
  block(block, _, _, timestamp).//交易所在区块
.output IntegerOverflow
```

**整数溢出（Integer Overflflows）：**通过检查来自CALLDATALOAD或CALLDATACOPY opcode的数据是否流入arithmetic运算来检测整数溢出，该算术结果与EVM返回的结果不匹配。之后，检查算术运算的结果是否流过SSTORE操作。